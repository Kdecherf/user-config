autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' actionformats '%F{5}[%f%s %i%b%F{red}|%a%f%i%m%F{5}]%f '
zstyle ':vcs_info:git*' formats '%F{5}[%f%s %i%b%m%F{5}]%f '
zstyle ':vcs_info:git*:*' check-for-changes true
# We explicitly disable get-revision to use %i for another purpose
zstyle ':vcs_info:git*:*' get-revision false

zstyle ':vcs_info:git*:*' stagedstr '✚'
zstyle ':vcs_info:git*:*' unstagedstr '⏺'

zstyle ':vcs_info:git*:*' patch-format ''

zstyle ':vcs_info:git*+set-message:*' hooks git-color git-misc-st git-misc-stage git-misc-stash git-icon-branch

# Set revision var to yellow color if there's unstaged changes
function +vi-git-color() {
   hook_com[revision]="%F{green}"
   if [[ ! -z ${hook_com[unstaged]} ]] ; then
      hook_com[revision]="%F{yellow}"
   fi
}

function +vi-git-icon-branch() {
   if [[ ${hook_com[branch]} =~ '^tags/(.+)' ]]; then
      hook_com[branch]="(${match[1]})"
   elif [[ ${hook_com[branch]} =~ '~|\^' ]]; then
      hook_com[branch]="<$(git rev-parse --short HEAD)>"
   fi
}

# Show remote ref name and number of commits ahead-of or behind
function +vi-git-misc-st() {
    local ahead behind remote
    local -a gitstatus

    # Are we on a remote-tracking branch?
    remote=${$(git rev-parse --verify ${hook_com[branch]}@{upstream} \
        --symbolic-full-name 2>/dev/null)/refs\/remotes\/}

    if [[ -n ${remote} ]] ; then
        ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
        (( $ahead )) && gitstatus+=( "⭡${ahead}" )

        behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
        (( $behind )) && gitstatus+=( "⭣${behind}" )

        if [[ $#gitstatus -gt 0 ]]; then
           hook_com[misc]+=" ${(j: :)gitstatus}"
        fi
    fi
}

function +vi-git-misc-stage() {
   local -a gitstage
   if [[ ! -z ${hook_com[unstaged]} ]] ; then
      gitstage+=( "${hook_com[unstaged]}" )
   fi
   if [[ ! -z ${hook_com[staged]} ]] ; then
      gitstage+=( "${hook_com[staged]}" )
   fi
   if [[ $#gitstage -gt 0 ]]; then
      hook_com[misc]+=" ${gitstage}"
   fi
}

# Show count of stashed changes
function +vi-git-misc-stash() {
    local -a stashes

    if [[ -s ${hook_com[base]}/.git/refs/stash ]] ; then
        #stashes=$(git stash list 2>/dev/null | wc -l)
        stashes=$(cat ${hook_com[base]}/.git/refs/stash | wc -l)
        #stashes=$(echo "\u$(printf "%X" $(( 0x2789 + ${stashes})))")

        hook_com[misc]+=" ✇${stashes}"
    fi
}

# http://eseth.org/2010/git-in-zsh.html
